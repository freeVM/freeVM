;  Licensed to the Apache Software Foundation (ASF) under one or more
;  contributor license agreements.  See the NOTICE file distributed with
;  this work for additional information regarding copyright ownership.
;  The ASF licenses this file to You under the Apache License, Version 2.0
;  (the "License"); you may not use this file except in compliance with
;  the License.  You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
;  Unless required by applicable law or agreed to in writing, software
;  distributed under the License is distributed on an "AS IS" BASIS,
;  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
;  See the License for the specific language governing permissions and
;  limitations under the License.
;
;
;

.class public org/apache/harmony/test/func/vm/verifier/field/Test09/Test0901/Test0901Ver
.super java/lang/Object
.field public testFieldB Lorg/apache/harmony/test/func/vm/verifier/field/Test09/Test0901/Test0901B;

;
; standard initializer
.method public <init>()V
   aload_0
   invokespecial java/lang/Object/<init>()V
   return
.end method

;
; test method
.method public test()V
   .limit stack 5
   .limit locals 3

; store 0 to localvariable2
   sipush 0
   istore_2

; push short, E, short
   sipush 10
   aload_0
   new org/apache/harmony/test/func/vm/verifier/field/Test09/Test0901/Test0901E
   dup
   invokespecial org/apache/harmony/test/func/vm/verifier/field/Test09/Test0901/Test0901E/<init>()V
   sipush 10

lab:
   iinc 2 1 ; localvariable2++
   pop
   putfield org/apache/harmony/test/func/vm/verifier/field/Test09/Test0901/Test0901Ver/testFieldB Lorg/apache/harmony/test/func/vm/verifier/field/Test09/Test0901/Test0901B;
   pop

   iload_2
   tableswitch 1
        l1 ; if 1 jump to l1
        l2 ; if 2 jump to l2
        l3 ; if 3 jump to l3
     default: def ; jump to def

l1:
; push float, E, float
   fconst_1
   aload_0
   new org/apache/harmony/test/func/vm/verifier/field/Test09/Test0901/Test0901E
   dup
   invokespecial org/apache/harmony/test/func/vm/verifier/field/Test09/Test0901/Test0901E/<init>()V
   fconst_1
   goto lab

l2:
; push Object, E, Object
   new java/lang/Object
   dup
   invokespecial java/lang/Object/<init>()V
   aload_0
   new org/apache/harmony/test/func/vm/verifier/field/Test09/Test0901/Test0901E
   dup
   invokespecial org/apache/harmony/test/func/vm/verifier/field/Test09/Test0901/Test0901E/<init>()V
   new java/lang/Object
   dup
   invokespecial java/lang/Object/<init>()V
   goto lab

l3:
; push E, E, E
   new org/apache/harmony/test/func/vm/verifier/field/Test09/Test0901/Test0901E
   dup
   invokespecial org/apache/harmony/test/func/vm/verifier/field/Test09/Test0901/Test0901E/<init>()V
   aload_0
   new org/apache/harmony/test/func/vm/verifier/field/Test09/Test0901/Test0901E
   dup
   invokespecial org/apache/harmony/test/func/vm/verifier/field/Test09/Test0901/Test0901E/<init>()V
   new org/apache/harmony/test/func/vm/verifier/field/Test09/Test0901/Test0901E
   dup
   invokespecial org/apache/harmony/test/func/vm/verifier/field/Test09/Test0901/Test0901E/<init>()V
   goto lab

def:
   return
.end method
